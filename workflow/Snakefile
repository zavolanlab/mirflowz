###############################################################################
# (c) 2020 Paula Iborra, Zavolan Lab, Biozentrum, University of Basel
# (@) paula.iborradetoledo@unibas.ch / paula.iborra@alumni.esci.upf.edu
#
# Snakemake workflow to:
#   - download and prepare the necessary files for smallRNA-seq related workflows.
#   - map small RNA-seq reads (e.g. from miRNA sequencing libraries)
#   - quantify miRNAs, including isomiRs, from miRNA-seq alignments.
#
###############################################################################

import os
import pandas as pd
from snakemake.utils import validate

###############################################################################
### Configuration validation
###############################################################################

validate(config, os.path.join("..", "config", "config_schema.json"))


###############################################################################
### Global configuration
###############################################################################


localrules:
    finish,


###############################################################################
### Including subworkflows
###############################################################################


include: os.path.join("rules", "prepare.smk")
include: os.path.join("rules", "map.smk")
include: os.path.join("rules", "quantify.smk")


###############################################################################
### Finish rule
###############################################################################


rule finish:
    input:
        idx_transcriptome=os.path.join(
            config["output_dir"],
            "transcriptome_index_segemehl.idx",
        ),
        idx_genome=os.path.join(
            config["output_dir"],
            "genome_index_segemehl.idx",
        ),
        exons=os.path.join(
            config["output_dir"],
            "exons.bed",
        ),
        header=os.path.join(
            config["output_dir"],
            "headerOfCollapsedFasta.sam",
        ),
        mirnafilt=os.path.join(
            config["output_dir"],
            "mirna_annotations.bed",
        ),
        maps=expand(
            os.path.join(
                config["output_dir"],
                "{sample}",
                "convertedSortedMappings_{sample}.bam.bai",
            ),
            sample=pd.unique(samples_table.index.values),
        ),
        table=lambda wildcard:expand(
            os.path.join(
                config["output_dir"], 
                "TABLES", 
                "counts.{mir}.tab"
            ),
            mir=config["mir_list"],
        ),