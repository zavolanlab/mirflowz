configfile: "config.yaml"

localrules: finish

#################################################################################
### Finish rule
#################################################################################

rule finish:
	input:
		idx_transcriptome = os.path.join(config["output_dir"], "transcriptome_index_segemehl.idx"),
		idx_genome = os.path.join(config["output_dir"], "genome_index_segemehl.idx"),
		exons_bed = os.path.join(config["output_dir"], "exons.bed"),
		header = os.path.join(config["output_dir"], "headerOfCollapsedFasta.sam")


#################################################################################
### DOWNLOAD DATA 
#################################################################################




#################################################################################
### Trim transcript IDs from FASTA file and get list IDs. 
#################################################################################

rule trim_fasta:
	input:
		fasta = config["transcriptome"]
	output:
		idlist = os.path.join(config["output_dir"], "idlist_fasta.txt"),
		fasta = os.path.join(config["output_dir"], "transcriptome_idtrim.fa")
	log:
		os.path.join(config["local_log"], "trim_fasta.log")
	shell:
		"(python scripts/validation_fasta.py --trim=\"\\.\" --idlist {output.idlist} -i {input.fasta} -o {output.fasta}) &> {log}"

#################################################################################
### Filter GTF and get ID list
#################################################################################

rule filter_gtf:
	input:
		idlist = os.path.join(config["output_dir"], "idlist_fasta.txt"),
		gtf = config["gtf"]
	output:
		idlist = os.path.join(config["output_dir"], "idlist_gtf.txt"),
		gtf = os.path.join(config["output_dir"], "gtf_filtered.gtf")
	singularity:
		"docker://zavolab/python_htseq:3.6.5_0.10.0"
	log:
		os.path.join(config["local_log"], "filter_gtf.log")
	shell:
		"(python scripts/validation_gtf.py --idlist {output.idlist} -f {input.idlist} -i {input.gtf} -o {output.gtf}) &> {log}"

#################################################################################
### Filter FASTA file
#################################################################################

rule filter_fasta:
	input:
		fasta = os.path.join(config["output_dir"], "transcriptome_idtrim.fa"),
		idlist = os.path.join(config["output_dir"], "idlist_gtf.txt"),
	output:
		fasta = os.path.join(config["output_dir"], "transcriptome_filtered.fa")
	log:
		os.path.join(config["local_log"], "filter_fasta.log")
	shell:
		"(python scripts/validation_fasta.py --filter {input.idlist} -i {input.fasta} -o {output.fasta}) &> {log}"


#################################################################################
### Generate segemehl index for transcripts
#################################################################################

rule generate_segemehl_index_transcriptome:
	input:
		fasta = os.path.join(config["output_dir"], "transcriptome_filtered.fa")
	output:
		idx = os.path.join(config["output_dir"], "transcriptome_index_segemehl.idx")
	log:
		os.path.join(config["local_log"], "generate_segemehl_index_transcriptome.log")
	singularity:
		"docker://zavolab/segemehl:0.2.0"
	shell:
		"(segemehl.x -x {output.idx} -d {input.fasta}) &> {log}"


#################################################################################
### Generate segemehl index for genome
#################################################################################

rule generate_segemehl_index_genome:
	input:
		sequence = config["genome"]
	output:
		idx = os.path.join(config["output_dir"], "genome_index_segemehl.idx")
	log:
		os.path.join(config["local_log"], "generate_segemehl_index_genome.log")
	singularity:
		"docker://zavolab/segemehl:0.2.0"
	shell:
		"(segemehl.x -x {output.idx} -d {input.sequence}) &> {log}"


#################################################################################
### GTF file of exons (genomic coordinates)
#################################################################################

rule get_exons_gtf:
	input:
		gtf = os.path.join(config["output_dir"], "gtf_filtered.gtf")
	output:
		exons = os.path.join(config["output_dir"], "exons.gtf")
	log:
		os.path.join(config["local_log"], "get_exons_gtf.log")
	singularity:
		"docker://ubuntu:18.04"
	shell:
		"(awk '$3 == \"exon\"' {input.gtf} > {output.exons} ) &> {log}"


#################################################################################
### Convert GTF file of exons to BED file
#################################################################################

rule gtftobed:
	input:
		exons = os.path.join(config["output_dir"], "exons.gtf")
	output:
		exons = os.path.join(config["output_dir"], "exons.bed")
	log:
		os.path.join(config["local_log"], "gtftobed.log")
	singularity:
		"docker://zavolab/r-zavolab:3.5.1"
	shell:
		"(Rscript scripts/gtf_exons_bed.1.1.2.R --gtf {input.exons} -o {output.exons}) &> {log}"



#################################################################################
### Create header for SAM file
#################################################################################

rule create_header_genome:
	input:
		genome = config["genome"]
	output:
		header = os.path.join(config["output_dir"], "headerOfCollapsedFasta.sam")
	log:
		os.path.join(config["local_log"], "create_header_genome.log")
	singularity:
		"docker://zavolab/samtools:1.8"
	shell:
		"(samtools dict -o {output.header} {input.genome}) &> {log}"










